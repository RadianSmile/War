final int BANANA = 1;
final int HAMI = 2;
final int GAMESTART = 0;
final int GAMERUN = 1;
final int GAMEOVER = 2;

PImage bg;
PImage bgStart;
PImage bgOver;
int wallet;
int currFruit;
int gameState;

Button hami;
Button banana;

Money money;
FruitMgr fruitMgr;

int zombieNum;
int zombieInterval;
Timer zombieTimer;
ArrayList<Milk> milks;

void setup(){
  size(640,480);
  
  bg = loadImage("img/bg_play.jpg");  // initialize
  bgStart = loadImage("img/bg_start.jpg");
  bgOver = loadImage("img/bg_lose.jpg");
  wallet = 0; // assignment operator
  currFruit = BANANA;
  gameState = GAMESTART;
  hami = new Button("hami_btn", 235,10);
  banana = new Button("banana_btn",300,10);
  money = new Money();
  
  fruitMgr = new FruitMgr();
  
  zombieInterval = 2000;  //ms
  zombieTimer = new Timer(zombieInterval);

  zombieNum = 10;
  milks = new ArrayList<Milk>();
}

void draw(){
  switch (gameState){
    case GAMESTART:
      image(bgStart,0,0);
      
      textSize(20);
      textAlign(CENTER);
      fill(255,0,0);  // fill red color
      text("Press 's' to Start", width/2,height*4/5);
      
      break;
    case GAMERUN:
      image(bg,0,0);  // use
      
      textSize(20);
      textAlign(RIGHT);
      fill(0);  // fill color 0-255: grey scale
      text(wallet, 210,50);
      
      if (currFruit == BANANA){
        hami.on = false;
        banana.on = true;
      }else{
        hami.on = true;
        banana.on = false;
      }
      hami.display();
      banana.display();
      // hit detection
      if (mouseX > money.x && mouseY > money.y &&
          mouseX < money.x+money.w && mouseY < money.y+money.h){
        wallet += 50;
        money.restart();
      }
      
      fruitMgr.display();

      // zombie milks
      if (zombieTimer.isEnd() && milks.size() < zombieNum){
        int x = Util.getXbyCol(7);
        int y = Util.getYbyRow(int(random(4)));
        milks.add(new Milk(x,y));
        zombieTimer.start();
      }
      for (int i=0; i < milks.size(); i++){
        Milk m = milks.get(i);
        m.move();
        // hit detection          
        if (fruitMgr.isHitFruit(m.x,m.y,m.w,m.h)) {
          println("hit fruit");
        }
        if (fruitMgr.isHitBullet(m.x,m.y,m.w,m.h)) {
          m.hurt();
          if (m.isDead()){
            milks.remove(i);
          }
        }
        // pass the line
        if (m.x < Util.left){
          gameState = GAMEOVER;
        }
      }

      money.drop();
      
      
      break;
    case GAMEOVER:
      image(bgOver,0,0);
      
      textSize(20);
      textAlign(CENTER);
      fill(255,0,0);  // fill red color
      text("Press 's' to Restart", width/2,height*4/5);
      break;
  }


}

void keyPressed(){
  // for practice 
  switch (gameState){
    case GAMESTART:
    case GAMEOVER:
      if (key == 's'){
        gameState = GAMERUN;
      }
      break;
  }

  if (key == 'e'){
    gameState = GAMEOVER;
  }
}


void mousePressed(){
  // for practice 
  if (gameState == GAMERUN){
    if (mouseX > hami.x && mouseY > hami.y &&
        mouseX < hami.x+hami.w && mouseY < hami.y+hami.h){
          // hit hami
          currFruit = HAMI;
    }
    if (mouseX > banana.x && mouseY > banana.y &&
        mouseX < banana.x+banana.w && mouseY < banana.y+banana.h){
          // hit banana
          currFruit = BANANA;
    }
    if (Util.isXYInGrid(mouseX,mouseY)){
      int col = Util.getColbyX(mouseX);
      int row = Util.getRowbyY(mouseY);
      //println(col,row);

      if (wallet >= 100 && currFruit == HAMI){
        boolean ok = fruitMgr.add(HAMI, col, row);
        if (ok) wallet -= 100;
      }else if (wallet >= 50 && currFruit == BANANA){
        boolean ok = fruitMgr.add(BANANA, col, row);
        if (ok) wallet -= 50;
      }
    }
  }
}